/**
 * gradle 主文件
 */
import java.text.SimpleDateFormat
import org.apache.tools.ant.taskdefs.condition.Os

ext {
    project_group = 'com.github.star45'
    project_name = '2me'
    project_version = '0.0.1-SNAPSHOT'
    project_description = '对xxx的 学习讨论。。。'
    project_jdk = '1.6'
    project_web = '2.5'
}
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'

group      = project_group
version    = project_version
description= project_description
apply from: 'dependency.gradle'


subprojects { subproj ->

    // 这儿置为空值，否则 idea 生成的依赖包命名以此值开始，与 gradle 的打包规则有冲突
    // group = ""
    group = "com.github.star45.2me"
    
    version = project_version
    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    repositories {
        maven { url 'http://maven.oschina.net/content/groups/public'}
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    dependencies {
        // 通用依赖
        compile(
            libraries.'ant',
            libraries.'antlr',
            libraries.'commons-beanutils',
            libraries.'commons-betwixt',
            libraries.'commons-codec',
            libraries.'commons-collections',
            libraries.'commons-digester',
            libraries.'commons-fileupload',
            libraries.'commons-io',
            libraries.'commons-lang',
            libraries.'commons-pool2',
            libraries.'log4j',
            
            libraries.'spring-aop',
            libraries.'spring-beans',
            libraries.'spring-aspects',
            libraries.'spring-context',
            libraries.'spring-web',
            libraries.'spring-orm',
            libraries.'spring-tx',
            
            libraries.'standard',
            libraries.'json'
        )
        
        // 引入根目录和各子项目下的libs中的jar包
        ext.jarTree = fileTree(dir: 'libs', include: '**/*.jar')
        ext.rootProjectLibs = new File(rootProject.rootDir, 'libs').getAbsolutePath()
        ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')

        compile jarTree

        // 测试依赖
        testCompile (
            libraries.'junit',
            libraries.'spring-test'
        )
    }
    
    // 显示当前项目下所有用于 compile 的 jar.
    task listJars(description: 'Display all compile jars.') << {
        configurations.compile.each { File file -> println file.name }
    }
    tasks.withType(JavaCompile) {
       sourceCompatibility = project_jdk
       targetCompatibility = project_jdk
    }
    task wrapper(type: Wrapper) { gradleVersion = '2.2.1' }
    eclipse {
        classpath { downloadSources=true }
    }
    sourceSets{
        main{
            java{srcDir "src/main/java"}
            resources{srcDir "src/main/resources"}
        }
        test{
            java{srcDir "src/test/java"}
            resources{srcDir "src/test/resources"}
        }
    }
}




